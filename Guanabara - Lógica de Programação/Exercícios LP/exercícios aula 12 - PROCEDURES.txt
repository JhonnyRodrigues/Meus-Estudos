algoritmo "Detector_de_Pesado"
 //função: aprendendo comando Procedimento (faz chamadas de linhas)
// Autor : Guanabara
// Data : 07/06/2019

var
   I: inteiro
   N, Pesado: caractere
   P, Mai: Real
PROCEDIMENTO Topo ()   // "topo" é um nome qualquer adotado
inicio                 // o parenteses é importante!
      Limpatela
      EscrevaL ("-------------------------------------------")
      EscrevaL ("--- D E T E C T O R   D E   P E S A D O ---")
      EscrevaL ("Maior peso até agora: ", Mai, " Kg")
      EscrevaL ("-------------------------------------------")
FimProcedimento
inicio
      Topo ()   // cabeçalho
      Para I <- 1 ate 5 faca
           Escreva ("Digite o nome: ")
           Leia (N)
           Escreva ("Digite o peso de ", N, ": ")
           Leia (P)
           Se (P>Mai) entao
              Mai <- P
              Pesado <- N
           FimSe
           Topo ()    // cabeçalho
      FimPara
      Topo ()          // cabeçalho
      EscrevaL ("A pessoa mais pesada foi ", Pesado, ", com ", Mai, " quilos.")

fimalgoritmo


------------------------------------------------------------------------------------


algoritmo "FazSomaProcedimento"

var
   X, Y: inteiro
Procedimento Soma (A, B: inteiro)
Inicio
      EscrevaL ("Recebi o valor ", A)
      EscrevaL ("Recebi o valor ", B)
      EscrevaL ("A soma entre os dois é ", A + B)
FimProcedimento
inicio
X <- 5
Y <- 3
Soma (X, Y)
fimalgoritmo


---------------------------------------------------------------------------------


algoritmo "Par_ou_ímpar_procedimento"

var
   N: inteiro
Procedimento ParOuImpar (V: inteiro)
inicio
      Se (V%2=0) entao
          EscrevaL ("O número ", V, " é PAR")
      senao
          EscrevaL ("O número ", V, " é ÍMPAR")
      FimSe
FimProcedimento
inicio
      Escreva ("digite um número: ")
      Leia (N)     // é bom colocar uma variavel diferente do parametro
      ParOuImpar (N)
fimalgoritmo


------------------------------------------------------------------------------------


algoritmo "Soma_Parâmetros-referência"   // *leia nota abaixo

var
   X, Y: inteiro    // variaveis globais
Procedimento Soma ( A, B: inteiro)  // Note que nao contem a palavra 'var'
inicio                             // portanto, não é uma REFERÊNCIA
      A <- A + 1                   // sendo assim, A e B recebem
      B <- B + 2                   // os VALORES de X e Y
      EscrevaL ("Valor de A = ", A)
      EscrevaL ("Valor de B = ", B)
      EscrevaL ("Soma A + B = ", A+B)
FimProcedimento
inicio
      X <- 4
      Y <- 8                    // X e y São variáveis de ESCOPO global
      Soma (X, Y)               // diferente de A e B que são local
      EscrevaL ("Valor de X = ", X)
      EscrevaL ("Valor de Y = ", Y)  // PERCEBA QUE X:4, Y:8, A: 5 e B:10
fimalgoritmo !!!<-
                      | |
                      V V

  //NOTA*------------------------------------------------------------------------
algoritmo "Soma_Parâmetros-referência"

var
   X, Y: inteiro
Procedimento Soma (var A, B: inteiro) // agora A e B não são mais valores
inicio                                 // e sim referências!
      A <- A + 1                       // e isso faz toda a diferença
      B <- B + 2                       // no resultado final
      EscrevaL ("Valor de A = ", A)
      EscrevaL ("Valor de B = ", B)
      EscrevaL ("Soma A + B = ", A+B)
FimProcedimento
inicio
      X <- 4
      Y <- 8
      Soma (X, Y)  //chamando o procedimento, os valores de X e Y serao
      EscrevaL ("Valor de X = ", X)     // copiados para dentro de A e B
      EscrevaL ("Valor de Y = ", Y)     // e no fim do procedimento, como
      // existe uma referencia ('var'), ela vai fazer com que o valor do
      // parametro A passe para X e o valor de B passe para Y
      // LOGO, X VAI VALER 5 E Y VAI VALER 10


__________________________________________________________________________________



algoritmo "Sequencia_Fibonacci_Parametro"

// Autor :   Guanabara
// Data : 10/06/2019

var
   C, T1, T2, T3: Inteiro
   
Procedimento ProximoFibonacci (var A, B: Inteiro)
var C: inteiro
Inicio
      C <-A + B
      Escreva (C)
      A <- B
      B <- C
FimProcedimento

inicio
      T1 <- 0
      Escreva (T1)
      T2 <- 1
      Escreva (T2)
      Para C <- 3 ate 10 faca      // começa do 3 pq dois termos ja foram escritos
           ProximoFibonacci (T1, T2)
      FimPara
fimalgoritmo